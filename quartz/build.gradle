plugins {
    id 'java-library'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = "$project.name"
                description = 'Quartz Enterprise Job Scheduler'
                url = 'https://www.quartz-scheduler.org/'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com:quartz-scheduler/quartz.git'
                    developerConnection = 'scm:git:ssh:git@github.com:quartz-scheduler/quartz.git'
                    url = 'https://github.com/quartz-scheduler/quartz'
                }
            }

            from components.java
        }
    }
}

dependencies {
    compileOnly project(':quartz-stubs')

    implementation "org.slf4j:slf4j-api:$slf4jVersion"
    runtimeOnly "org.slf4j:slf4j-log4j12:$slf4jVersion"
    implementation "com.mchange:c3p0:$c3p0Version"
    implementation "com.zaxxer:HikariCP:$hikaricpVersion"
    compileOnly "jboss:jboss-common:$jbossVersion"
    compileOnly "jboss:jboss-minimal:$jbossVersion"
    compileOnly "jboss:jboss-system:$jbossVersion"
    compileOnly "jboss:jboss-jmx:$jbossVersion"
    compileOnly "org.apache.geronimo.specs:geronimo-commonj_1.1_spec:$commonjVersion"

    implementation 'javax.xml:jaxb-api:2.1'

    compileOnly 'javax.transaction:javax.transaction-api:1.3'
    compileOnly 'javax.servlet:javax.servlet-api:4.0.1'

    testImplementation "junit:junit:$junitVersion"
    testImplementation 'asm:asm:3.2'
    testImplementation 'org.mockito:mockito-core:1.9.5'
    testImplementation 'org.hamcrest:hamcrest-library:1.2'

    testImplementation "org.apache.derby:derby:$derbyVersion"
    testImplementation "org.apache.derby:derbynet:$derbyVersion"
    testImplementation "org.apache.derby:derbyclient:$derbyVersion"
}

test {
    maxParallelForks 1
    forkEvery 1
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options {
        docTitle = "Quartz Enterprise Job Scheduler $project.version API"
        encoding = 'UTF-8'
        outputLevel = JavadocOutputLevel.QUIET
        splitIndex = false
        author = true;
        windowTitle = "Quartz Enterprise Job Scheduler $project.version API"
        bottom = 'Copyright 2001-2023, Terracotta, Inc.'
    }
}


processResources {
    filesMatching('**/quartz-build.properties') {
        expand([version: project.version, fullname: project.fullname, name: project.name])
    }
}
